# -*- Mode: Python -*-

import itertools
import os
import shlex

common_cc_flags = "-Wall -O2 -g -pipe -ffreestanding -nostdinc -mno-avx -mno-avx2 -fno-asynchronous-unwind-tables -fno-stack-protector"
common_cxx_flags = "-std=c++14 -fno-threadsafe-statics -fno-rtti -fno-exceptions -nostdinc++"

common_bare_env = Environment(CXX=os.environ.get("CXX", "clang++"),
                              CC=os.environ.get("CC", "clang"),
                              LINK=os.environ.get("CXX", "clang++"),
                              AR=os.environ.get("AR", "llvm-ar"),
                              ENV = os.environ,
                              AS="nasm",
                              CCFLAGS=common_cc_flags,
                              CXXFLAGS=common_cxx_flags,
                              OBJSUFFIX=".${ARCH_NAME}.o",
                              PROGSUFFIX=".${ARCH_NAME}.elf",
                              ASFLAGS="-O5",
                              CPPPATH=["#$ARCH_NAME/include", "#common/include", "#include"],
                              LINKFLAGS="-nostdlib -g -Xlinker -n -Xlinker -T -Xlinker")

if "BARESIFTER_VERSION" in os.environ:
    version_cmd = "echo {}".format(shlex.quote(os.environ["BARESIFTER_VERSION"]))
else:
    version_cmd = "git describe --always --dirty"

version_inc = common_bare_env.Command("common/version.inc", [],
                                      version_cmd + " | sed -E 's/^(.*)$/\"\\1\"/' > $TARGET")
AlwaysBuild(version_inc)

bare64_env = common_bare_env.Clone(ARCH_NAME="x86_64")
bare64_env.Append(CCFLAGS=" -m64 -march=x86-64 -mno-red-zone",
                  ASFLAGS=" -felf64")

bare32_env = common_bare_env.Clone(ARCH_NAME="x86_32")
bare32_env.Append(CCFLAGS=" -m32 -march=i386 -mregparm=3 -fomit-frame-pointer",
                  ASFLAGS=" -felf32")

bins = []
for env in [bare64_env, bare32_env]:
    source_directories = [".", "common", "musl", "$ARCH_NAME"]
    if env["ARCH_NAME"] == "x86_32":
        source_directories += ["compiler-rt"]

    source_files = [files
                    for directory in source_directories
                    for extension in ["c", "cpp", "asm"]
                    for files in env.Glob("{}/*.{}".format(directory, extension)) ]

    bins.append(env.Program(target="baresifter", source = ["$ARCH_NAME/standalone.lds"] + source_files))


# Installation

install_env = Environment(PREFIX=ARGUMENTS.get("prefix", "/usr/local"))

Alias("install", install_env.Install("$PREFIX/share/baresifter", bins))
